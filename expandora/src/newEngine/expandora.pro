# File generated by kdevelop's qmake manager. 
# ------------------------------------------- 
# Subdir relative project main directory: ./src/newEngine
# Target is an application:  
# Target is an application:  


SUBDIRS += Common \
           Lexer \
           Parser \
           Map \
	   Storage \
           Filter \
	   Display \
	   Configuration \
	   Proxy \
	   expandora


CONFIG += debug thread qt warn_on monolithic

monolithic {

ORIG_LIBS = $$LIBS
ORIG_CONFIG = $$CONFIG
ORIG_QT = $$QT

for(dir, SUBDIRS) {
  LAST_SOURCES = $$SOURCES
  SOURCES = 
  LAST_HEADERS = $$HEADERS
  HEADERS =
  LAST_LEX = $$LEXSOURCES
  LEXSOURCES =
  include($${dir}/$${dir}.pro)

  for(source, SOURCES){
    LAST_SOURCES += $${dir}/$${source} 
  }
  for(header, HEADERS){
    LAST_HEADERS += $${dir}/$${header}
  }
  HEADERS = $$LAST_HEADERS
  for(source, LEXSOURCES){
    LAST_LEX += $${dir}/$${source}
  }
  
  LEXSOURCES = $$LAST_LEX
  SOURCES = $$LAST_SOURCES
  HEADERS = $$LAST_HEADERS
}

LIBS = $$ORIG_LIBS
CONFIG = $$ORIG_CONFIG
QT = $$ORIG_QT

INCLUDEPATH = include

TEMPLATE = app

OBJECTS_DIR	= obj


win32 {
	CONFIG	+= console
}
QT += opengl network xml

DESTDIR = bin

DEFINES += MONOLITHIC


debug {
 # DEFINES += DMALLOC
 # LIBS += -lmpatrolmt -lelf -lmpalloc -lbfd
}

}

modular {
  TEMPLATE = subdirs
}